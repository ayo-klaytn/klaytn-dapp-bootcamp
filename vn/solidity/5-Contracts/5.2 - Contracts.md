# Tìm hiểu thêm về Contract của Solidity

## constructor
Constructor là function chạy ngay lập tức khi smart contract được khởi tạo

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

// Base contract X
contract X {
    string public name;

    constructor(string memory _name) {
        name = _name;
    }
}
```

## State variable visibility
- `public` - biến public giống biến `internal` (cho phép contract hiện tại và các contract thừa hưởng truy cập) nhưng sẽ tự động tạo ra `getter function` để các contract ngoài có thể truy cập luôn.
- `internal` - biến chỉ có thể được truy cập bởi contract hiện tại và các contract thừa hưởng. Đây cũng là default visibility cho state variable.
- `private` - biến chỉ có thể được truy cập bởi contract hiện tại.

**Note**: Biến `internal` và `private` chỉ hạn chế truy cập tới `contract` khác. Giá trị của biến vẫn có thể được thấy bởi tất cả mọi người.

## Function visibility
- `external` - `function` chỉ có thể được gọi từ ngoài vào.
- `public` - `function` vừa có thể được gọi bằng `function` khác trong `contract`, vừa có thể được gọi từ ngoài vào.
- `internal` - `function` chỉ có thể được gọi bởi `contract` hiện tại hoặc `contract` thừa hưởng.
- `private` - `function` chỉ có thể được gọi bởi `contract` hiện tại.

## Getter function
`function` dùng để gọi vào biến `public` mà compiler tự động tạo. Cũng được sử dụng để chỉ khái niệm `function` dùng để query biến để xem.
```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.4.16 <0.9.0;

contract C {
    uint public data = 42;
}

contract Caller {
    C c = new C();
    function f() public view returns (uint) {
        return c.data();
    }
}
```

## Constant và immutable state variables
- `constant` - biến có giá trị được cố định ngay khi compile (cho vào contract bytecode).
- `immutable` - biến có giá trị có thể được gán vào trong thời gian `construct`.

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.21;

uint constant X = 32**22 + 8;

contract C {
    string constant TEXT = "abc";
    bytes32 constant MY_HASH = keccak256("abc");
    uint immutable decimals = 18;
    uint immutable maxBalance;
    address immutable owner = msg.sender;

    constructor(uint decimals_, address ref) {
        if (decimals_ != 0)
            // Immutables are only immutable when deployed.
            // At construction time they can be assigned to any number of times.
            decimals = decimals_;

        // Assignments to immutables can even access the environment.
        maxBalance = ref.balance;
    }

    function isBalanceTooHigh(address other) public view returns (bool) {
        return other.balance > maxBalance;
    }
}
```
## Pure function
`function` không đọc hoặc thay đổi state của blockchain. Hay được sử dụng làm `function` tính toán.
```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.5.0 <0.9.0;

contract C {
    function f(uint a, uint b) public pure returns (uint) {
        return a * (b + 42);
    }
}
```

## Payable functions and addresses

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract Payable {
    // Payable address can send Ether via transfer or send
    address payable public owner;

    // Payable constructor can receive Ether
    constructor() payable {
        owner = payable(msg.sender);
    }

    // Function to deposit Ether into this contract.
    // Call this function along with some Ether.
    // The balance of this contract will be automatically updated.
    function deposit() public payable {}

    // Call this function along with some Ether.
    // The function will throw an error since this function is not payable.
    function notPayable() public {}

    // Function to withdraw all Ether from this contract.
    function withdraw() public {
        // get the amount of Ether stored in this contract
        uint256 amount = address(this).balance;

        // send all Ether to owner
        (bool success,) = owner.call{value: amount}("");
        require(success, "Failed to send Ether");
    }

    // Function to transfer Ether from this contract to address from input
    function transfer(address payable _to, uint256 _amount) public {
        // Note that "to" is declared as payable
        (bool success,) = _to.call{value: _amount}("");
        require(success, "Failed to send Ether");
    }
}
```

## Receive Ether and Fallback function
A contract can have at most one receive function, declared using receive() external payable { ... } (without the function keyword). This function cannot have arguments, cannot return anything and must have external visibility and payable state mutability. It can be virtual, can override and can have modifiers.

Một `contract` có thể có tối đa một `receive` function, được khai báo bằng cách sử dụng `receive() external payable { ... }` (không có từ khóa `function`). `function` này không được có `argument`, không được `return` bất kỳ thứ gì và phải có `external` visibility cũng như `payable` `state mutability`. Nó có thể là `virtual`, có thể `override` và có thể có các `modifier`.

    Ether được gửi tới contract
           msg.data có trống không?
             /   \ 
            có  không
            /     \
       receive()?  fallback() 
        /   \ 
      có   không
     /        \
  receive()  fallback()

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract Fallback {
    event Log(string func, uint256 gas);

    // Fallback function must be declared as external.
    fallback() external payable {
        // send / transfer (forwards 2300 gas to this fallback function)
        // call (forwards all of the gas)
        emit Log("fallback", gasleft());
    }

    // Receive is a variant of fallback that is triggered when msg.data is empty
    receive() external payable {
        emit Log("receive", gasleft());
    }

    // Helper function to check the balance of this contract
    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }
}

contract SendToFallback {
    function transferToFallback(address payable _to) public payable {
        _to.transfer(msg.value);
    }

    function callFallback(address payable _to) public payable {
        (bool sent,) = _to.call{value: msg.value}("");
        require(sent, "Failed to send Ether");
    }
}
```

## Oracle

Oracle cho smart contract là cầu nối giữa blockchain và thế giới bên ngoài. Nó cung cấp dữ liệu cho smart contract từ các nguồn bên ngoài blockchain, chẳng hạn như API, dữ liệu thị trường, dữ liệu thời tiết, v.v...

Dưới đây là một số ví dụ về cách sử dụng oracle cho smart contract:

- Cung cấp dữ liệu giá cho thị trường phi tập trung (DeFi): Oracle có thể cung cấp dữ liệu giá cho các tài sản tiền mã hóa, cho phép các nhà giao dịch thực hiện giao dịch trên các sàn giao dịch phi tập trung.
- Kích hoạt hợp đồng bảo hiểm: Oracle có thể cung cấp dữ liệu về các sự kiện bảo hiểm, chẳng hạn như tai nạn hoặc thiên tai, để kích hoạt thanh toán bảo hiểm.
- Tự động hóa các quy trình: Oracle có thể được sử dụng để tự động hóa các quy trình, chẳng hạn như thanh toán hóa đơn hoặc quản lý chuỗi cung ứng.

Danh sách các Oracle trên Ethereum:
https://www.alchemy.com/list-of/decentralized-oracles-on-ethereum